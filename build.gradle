buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        //classpath 'com.matthewprenger:CurseGradle:1.0-SNAPSHOT'
    }
}

apply plugin: "maven"
apply plugin: 'net.minecraftforge.gradle.forge'
//apply plugin: 'com.matthewprenger.cursegradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "${mc_version}-" + getVersion()
group = "codersafterdark.reskillable" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${mod_name}"

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

    coreMod = "codersafterdark.reskillable.base.asm.LoadingPlugin"

    mappings = "${mc_mappings}"
    replace 'GRADLE:VERSION', "${mc_version}"
    replace 'GRADLE:BUILD', "${mod_version}"

    // Scala: not even once
    replace 'scala.actors.threadpool.Arrays', 'java.util.Arrays'

    makeObfSourceJar = false
}

repositories{
    maven{
        name 'CraftTweaker'
        url 'http://maven.blamejared.com'
    }
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        url = "http://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name = "thiakil"
        url = "http://maven.thiakil.com"
    }
}

dependencies {
    //implementation("curse.maven:jei-238222:3040523")
    deobfCompile("curse.maven:astral-sorcery-241721:3044416")
    deobfCompile("curse.maven:baubles-227083:2518667")
    deobfCompile("curse.maven:jei-238222:3040523")
    deobfCompile("curse.maven:serene-seasons-291874:2799213")
    deobfCompile("curse.maven:tipthescales-282313:2689835")
    //deobfCompile "mezz.jei:jei_1.12.2:4.13.1.220"
    compile files('lib/DynamicSwordSkills-1.12.2-6.2.1.jar')
    compile files('lib/SwordSkillsApi-1.12.2-1.5.1.jar')
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
        }
    }
}

jar {
    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.exe"
    exclude "**/unused"

    manifest {
        attributes("FMLCorePluginContainsFMLMod": "true", "FMLCorePlugin": "codersafterdark.reskillable.base.asm.LoadingPlugin")
        attributes "FMLAT": "reskillable_at.cfg"
    }

}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': "${mc_version}"
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', '**/psd/**'
    }
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives srcJar
}

String getVersion() {
    String version = "${mod_version}"
    String branch = project.hasProperty("branch") ? project.branch : ""

    if(branch != "") {
        version += "-" + branch

        String buildNumber = getBuildNumber()
        if(buildNumber != "") {
            version += "." + buildNumber
        }
    }

    return version
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if(buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? "." + System.getenv("bamboo_buildNumber") : ""
    }
    return buildNumber
}


