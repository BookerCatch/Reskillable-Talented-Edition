buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}
        
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${mc_version}-" + getVersion()
group = "codersafterdark.reskillable" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'reskillable'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    accessTransformer = file('src/main/resources/META-INF/reskillable_at.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                reskillable {
                    sources = [sourceSets.main]
                }
            }
        }

        server {

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += 'resources'
        }
    }
}

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'

    apiElements fg.deobf("curse.maven:baubles-227083:2518667")
    compileOnly fg.deobf("curse.maven:jei-238222:3040523")
    compileOnly fg.deobf("curse.maven:serene-seasons-291874:2799213")
    compileOnly fg.deobf("curse.maven:tipthescales-282313:2689835")
    compileOnly fg.deobf("curse.maven:dynamic-stealth-309344:3127982")
    compileOnly fg.deobf("curse.maven:fantastic-lib-315773:3138902")
    compileOnly fg.deobf("curse.maven:potion-core-242872:2905184")
    compileOnly fg.deobf("curse.maven:spartan-weaponry-278141:2830166")

    compile files('lib/DynamicSwordSkills-1.12.2-6.2.1.jar')
    compile files('lib/SwordSkillsApi-1.12.2-1.5.1.jar')
    compile files ('lib/CompatSkills-1.12.2-1.17.0.jar')

}

jar {
    manifest {
        attributes("FMLCorePluginContainsFMLMod": "true", "FMLCorePlugin": "codersafterdark.reskillable.base.asm.LoadingPlugin")
        attributes "FMLAT": "reskillable_at.cfg"
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

String getVersion() {
    String version = "${mod_version}"
    String branch = project.hasProperty("branch") ? project.branch : ""

    if(branch != "") {
        version += "-" + branch

        String buildNumber = getBuildNumber()
        if(buildNumber != "") {
            version += "." + buildNumber
        }
    }

    return version
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if(buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? "." + System.getenv("bamboo_buildNumber") : ""
    }
    return buildNumber
}

